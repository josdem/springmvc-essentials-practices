ext.springVersion = "4.0.0.RELEASE"

subprojects {
  apply plugin:"java"
  apply plugin: 'project-report'
  sourceCompatibility = 1.6
  version = '1.0'

  repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/release/" }
    maven { url "http://repo.spring.io/milestone/" }
  }
}

project(":repositoriesAndServices") {
  dependencies {
    compile "org.springframework:spring-context:$springVersion"
    compile "org.springframework:spring-orm:$springVersion"
    compile "org.springframework:spring-aspects:$springVersion"
    
    compile "org.hibernate:hibernate-entitymanager:4.3.1.Final"
    def excludesList = ['slf4j-api','jcl-over-slf4j','spring-orm','spring-context','spring-aop','spring-tx','spring-beans','spring-core']
    compile("org.springframework.data:spring-data-jpa:1.4.3.RELEASE"){
      excludesList.each { m -> exclude module:m }
    }
    compile 'org.slf4j:slf4j-log4j12:1.7.6'
    compile 'com.h2database:h2:1.3.175'
    
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile 'junit:junit:4.11'
  }
}

project(":web") {
  apply plugin: 'war'
  apply plugin:"jetty"

  //httpPort = 8081

  jettyRun {
    contextPath = 'trackbox'
    reload = 'automatic'
    scanIntervalSeconds = 10
  }
  dependencies {
    compile project(":repositoriesAndServices")
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.apache.commons:commons-lang3:3.2.1"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile("org.hibernate:hibernate-validator:5.0.3.Final"){
      exclude module:'jboss-logging'
    }
    providedCompile "javax.servlet:javax.servlet-api:3.1.0"
    runtime 'jstl:jstl:1.2'
    compile 'commons-fileupload:commons-fileupload:1.3.1'
  }

}